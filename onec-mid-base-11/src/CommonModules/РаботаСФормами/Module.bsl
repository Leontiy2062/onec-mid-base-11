///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2023, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////
#Область ПрограммныйИнтерфейс

// ++ Гревцов Задача ДЗ_ЕДТ_командная рабрта 15.07.25
//Параметры:
//		Форма - ФормаКлиентскогоПриложения - в которой будут отображаться дополнительные реквизиты со свойствами;
Процедура ДополнитьФорму(Форма) Экспорт
	// ++ Гревцов Задача ДЗ_ЕДТ_командная рабрта 15.07.25 Добавляем новый реквизит КонтактноеЛицо на формы всех документов,
	// содержащих реквизит Контрагент
	
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
		
	// Добавляем только на ЗаказПокупателя новый реквизит СогласованнаяСкидка и команду ПересчитатьТаблицу
		ДобавитьГруппаПолеСогласованнаяСкидкаИКомандаВГруппаШапкаЛево(Форма);
		
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
		
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
		
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		
		ДобавитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
		
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		
		ДобавитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
		
	КонецЕсли;
	// ++
КонецПроцедуры

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
		
	Если Форма.Элементы.Найти("КонтактноеЛицо") = Неопределено Тогда
		
		ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
		ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
		ПолеВвода.ПутьКДанным = "Объект.КонтактноеЛицо";
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма)
	
	Если Форма.Элементы.Найти("КонтактноеЛицо") = Неопределено Тогда
		
		ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"),, Форма.Элементы.СуммаДокумента);
		ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
		ПолеВвода.ПутьКДанным = "Объект.КонтактноеЛицо";
		
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьГруппаПолеСогласованнаяСкидкаИКомандаВГруппаШапкаЛево(Форма)
	// ++ Гревцов Задача ДЗ_ЕДТ_командная рабрта 15.07.25 Добавляем только на ЗаказПокупателя новый реквизит
	// СогласованнаяСкидка и команду ПересчитатьТаблицу
	Если Форма.Элементы.Найти("ГруппаСкидкаПересчет") = Неопределено Тогда
			
		ГруппаСкидкаПересчет = Форма.Элементы.Добавить("ГруппаСкидкаПересчет", Тип("ГруппаФормы"),
						Форма.Элементы.ГруппаШапкаЛево);
		ГруппаСкидкаПересчет.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаСкидкаПересчет.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
		
	КонецЕсли;
		
	Если Форма.Элементы.Найти("СогласованнаяСкидка") = Неопределено Тогда
		
		ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаСкидкаПересчет);
		ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
		ПолеВвода.ПутьКДанным = "Объект.СогласованнаяСкидка";
		ПолеВвода.УстановитьДействие("ПриИзменении", "Подключаемый_СогласованнаяСкидкаПриИзменении");
		
	КонецЕсли;
	
	Если Форма.Элементы.Найти("КнопкаПересчитатьТаблицу") = Неопределено Тогда
		
		КомандаПересчитатьТаблицу = Форма.Команды.Добавить("ПересчитатьТаблицу");
		КомандаПересчитатьТаблицу.Заголовок = "Пересчитать таблицу";
		КомандаПересчитатьТаблицу.Действие = "Подключаемый_КомандаПересчитатьТаблицу";
		
		КнопкаКомандыПересчитатьТаблицу = Форма.Элементы.Добавить("КнопкаПересчитатьТаблицу", Тип("КнопкаФормы"),
						Форма.Элементы.ГруппаСкидкаПересчет);
		КнопкаКомандыПересчитатьТаблицу.ИмяКоманды = "ПересчитатьТаблицу";
		КнопкаКомандыПересчитатьТаблицу.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
		КнопкаКомандыПересчитатьТаблицу.Отображение = ОтображениеКнопки.КартинкаИТекст;
		КнопкаКомандыПересчитатьТаблицу.Картинка = БиблиотекаКартинок.Обновить;
		
	КонецЕсли;
	// --
КонецПроцедуры

#КонецОбласти